apply plugin: 'com.android.application'

android {
    namespace 'com.quad9.aegis'
    compileSdk 35
    defaultConfig {
        applicationId "com.quad9.aegis"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode 103
        versionName "1.0.0"
        vectorDrawables.useSupportLibrary = true
        ndk {
            debugSymbolLevel 'SYMBOL_TABLE'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["store"]
    productFlavors {
        googleplay {
            dimension "store"
            setArchivesBaseName("quad9-aegis-gplay")
        }
        fdroid {
            dimension "store"
            setArchivesBaseName("quad9-aegis-fdroid")
        }
    }

    dataBinding.enabled = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    // Import the BoM for the Firebase platform
    googleplayImplementation platform('com.google.firebase:firebase-bom:33.9.0')

    // Add the dependency for the Analytics library
    // When using the BoM, you don't specify versions in Firebase library dependencies
    googleplayImplementation 'com.google.firebase:firebase-analytics-ktx'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.core:core-ktx:1.15.0'

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'

    implementation 'org.pcap4j:pcap4j-core:1.7.3'
    implementation 'org.pcap4j:pcap4j-packetfactory-static:1.7.3'
    implementation 'de.measite.minidns:minidns-core:0.2.4'
    implementation 'de.measite.minidns:minidns-hla:0.2.4'
    implementation 'org.slf4j:slf4j-simple:1.6.6'

    googleplayImplementation 'com.google.android.gms:play-services-plus:17.0.0'

    implementation 'ch.acra:acra-mail:5.9.0'
    implementation 'ch.acra:acra-dialog:5.9.0'

    implementation 'org.conscrypt:conscrypt-android:2.2.1'
    implementation 'com.github.AppIntro:AppIntro:v5.1.0'
    googleplayImplementation 'com.google.firebase:firebase-core:21.1.1'
    googleplayImplementation 'com.google.firebase:firebase-analytics'
    googleplayImplementation 'com.google.firebase:firebase-crashlytics'
}

if (gradle.startParameter.taskNames.any { task -> task.contains("Googleplay") }) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
}
